defmodule Mobilizon.Service.ICalendarTest do
  use Mobilizon.DataCase

  import Mobilizon.Factory

  alias ICalendar.Value

  alias Mobilizon.Addresses.Address
  alias Mobilizon.Events.{Event, FeedToken}
  alias Mobilizon.Service.Export.ICalendar, as: ICalendarService

  describe "export an event to ics" do
    test "export basic infos" do
      %Event{} = event = insert(:event)

      ics = """
      BEGIN:VCALENDAR
      CALSCALE:GREGORIAN
      VERSION:2.0
      PRODID:-//Elixir ICalendar//Mobilizon #{Mobilizon.Config.instance_version()}//EN
      BEGIN:VEVENT
      ATTACH;FMTTYPE=#{event.picture.file.content_type}:#{event.picture.file.url}
      CATEGORIES:#{Enum.map_join(event.tags, ",", & &1.title)}
      DESCRIPTION:Ceci est une description avec une première phrase assez longue\\,\\n      puis sur une seconde ligne
      DTEND:#{Value.to_ics(event.ends_on)}Z
      DTSTAMP:#{Value.to_ics(event.publish_at)}Z
      DTSTART:#{Value.to_ics(event.begins_on)}Z
      GEO:#{event.physical_address |> Address.coords() |> Tuple.to_list() |> Enum.join(";")}
      LOCATION:#{Address.representation(event.physical_address)}
      ORGANIZER:#{event.organizer_actor.url}
      STATUS:#{event.status |> to_string() |> String.upcase()}
      SUMMARY:#{event.title}
      UID:#{event.uuid}
      URL:#{event.url}
      END:VEVENT
      END:VCALENDAR
      """

      assert {:ok, ics} == ICalendarService.export_public_event(event)
    end
  end

  describe "export the instance's public events" do
    test "succeds" do
      %Event{} = event1 = insert(:event, title: "I'm public")
      %Event{} = event2 = insert(:event, visibility: :private, title: "I'm private")
      %Event{} = event3 = insert(:event, title: "Another public", picture: nil)
      %Event{} = event4 = insert(:event, title: "No description", description: nil)

      {:commit, ics} = ICalendarService.create_cache("instance")
      assert ics =~ event1.title
      refute ics =~ event2.title
      assert ics =~ event3.title
      assert ics =~ event4.title

      assert Enum.sort(Regex.scan(~r|ATTACH;FMTTYPE=image.*|, ics)) ==
               Enum.sort([
                 [
                   "ATTACH;FMTTYPE=#{event1.picture.file.content_type}:#{event1.picture.file.url}"
                 ],
                 [
                   "ATTACH;FMTTYPE=#{event4.picture.file.content_type}:#{event4.picture.file.url}"
                 ]
               ])
    end

    test "with 50 events" do
      Enum.each(0..50, fn i ->
        %Event{} = insert(:event, title: "Event #{i}")
      end)

      {:commit, ics} = ICalendarService.create_cache("instance")

      Enum.each(0..50, fn i ->
        assert ics =~ "Event #{i}"
      end)
    end
  end

  describe "export an actor's events from a token" do
    test "an actor feedtoken" do
      user = insert(:user)
      actor = insert(:actor, user: user)
      %FeedToken{token: token} = insert(:feed_token, user: user, actor: actor)

      event1 = insert(:event, title: "event owner", description: "owner", organizer_actor: actor)

      event2 =
        insert(:event, title: "event particiated", description: "particiated", picture: nil)

      event3 = insert(:event, visibility: :private, title: "I'm private")
      event4 = insert(:event, title: "No description", description: nil)

      insert(:participant, event: event2, actor: actor, role: :participant)

      {:commit, ics} = ICalendarService.create_cache("token_#{ShortUUID.encode!(token)}")
      refute ics =~ event4.title
      refute ics =~ event3.title
      assert ics =~ event2.title
      assert ics =~ event1.title
    end

    test "by actor preferred_username" do
      user = insert(:user)
      actor = insert(:actor, user: user)

      event1 = insert(:event, title: "event owner", description: "owner", organizer_actor: actor)

      event2 =
        insert(:event, title: "event particiated", description: "particiated", picture: nil)

      event3 = insert(:event, visibility: :private, title: "I'm private")
      event4 = insert(:event, title: "No description", description: nil)

      insert(:participant, event: event2, actor: actor, role: :participant)

      {:commit, ics} = ICalendarService.create_cache("actor_#{actor.preferred_username}")
      refute ics =~ event4.title
      refute ics =~ event3.title
      assert ics =~ event1.title
      assert ics =~ event2.title
    end

    test "by actor feedtoken complexe" do
      user = insert(:user)
      actor = insert(:actor, user: user)
      %FeedToken{token: token} = insert(:feed_token, user: user, actor: actor)

      event1 =
        insert(:event, title: "event simple owner", description: "owner", organizer_actor: actor)

      event2 =
        insert(:event, title: "event particiated", description: "particiated", picture: nil)

      event3 =
        insert(:event,
          title: "event owner and particiated",
          description: "owner & particiated",
          picture: nil,
          organizer_actor: actor
        )

      insert(:participant, event: event2, actor: actor, role: :participant)
      insert(:participant, event: event3, actor: actor, role: :participant)

      {:commit, ics} = ICalendarService.create_cache("token_#{ShortUUID.encode!(token)}")
      assert ics |> String.split(event1.title) |> length() == 2
      assert ics |> String.split(event2.title) |> length() == 2
      assert ics |> String.split(event3.title) |> length() == 2
    end

    test "by actor preferred_username complexe" do
      user = insert(:user)
      actor = insert(:actor, user: user)

      event1 =
        insert(:event, title: "event simple owner", description: "owner", organizer_actor: actor)

      event2 =
        insert(:event, title: "event particiated", description: "particiated", picture: nil)

      event3 =
        insert(:event,
          title: "event owner and particiated",
          description: "owner & particiated",
          picture: nil,
          organizer_actor: actor
        )

      insert(:participant, event: event2, actor: actor, role: :participant)
      insert(:participant, event: event3, actor: actor, role: :participant)

      {:commit, ics} = ICalendarService.create_cache("actor_#{actor.preferred_username}")
      assert ics |> String.split(event1.title) |> length() == 2
      assert ics |> String.split(event2.title) |> length() == 2
      assert ics |> String.split(event3.title) |> length() == 2
    end
  end
end
